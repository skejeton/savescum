import ("th.um"; "rect.um"; "canvas.um"; "input.um"; "image.um"
  "misc/mstrings.um"; "mdbg.um";
  th_color = "color.um";
  "d_rerp.um"; "d_background.um"; "d_resources.um"; 
  "d_util.um"; "d_world.um"; "d_global.um"; "d_worlddata.um")

const (
  shapeBlock          = 0
  shapeLeftUpSlope    = 1
  shapeRightUpSlope   = 2
  shapeLeftDownSlope  = 3
  shapeRightDownSlope = 4
)

// 125 updates per second
const updateStep = 1/60.0; 
const updateStepMs = 16;

type (
  Shape* = struct {
    rect: rect.Rect
    shape: int
  }

  Tag* = struct {
    isSet: bool
    param: str
  }

  TagSet* = struct {
    tags: map[str]Tag
  }

  Character* = struct {
    id: str
    tags: TagSet
    originalRect: rect.Rect
    rect: rect.Rect
    vel: th.Vf2
    onGround: bool
    onSlope: bool
  }
)

fn (t: ^TagSet) set*(tag: str) {
  params := mstrings.splitBy(tag, ':')
  if len(params) > 1 {
    t.tags[params[0]].param = params[1]
  }
  t.tags[params[0]].isSet = true
}

fn (t: ^TagSet) param*(tag: str): str {
  return t.tags[tag].param
}

fn (t: ^TagSet) isSet*(tag: str): bool {
  return validkey(t.tags, tag) && t.tags[tag].isSet
} 

fn initTagSetFromString*(s: str): TagSet {
  tagSet := TagSet{map[str]Tag{}}
  tags := mstrings.splitBy(s, ';')
  for i, tag in tags {
    tagSet.set(tag)
  }

  return tagSet
}

type ChumState = struct {
  dir: int
  rotTime: real
  walkTime: real
  ent: Character
  toggleActiveStatic: bool
}

var (
  // Resources:
  chumDir: int
  chumRotTime: real
  chumWalkTime: real
  imgObj: image.Image
  imgChum: image.Image

  lockInput*: bool
  guides: []rect.Rect
  entities*: []Character
  sceneId*: int
  world*: d_world.World
  chara*: Character
  camera*: th.Vf2
  cameraScale*: real
  toggleActive: bool
  toggleActiveVal: real
  toggleActiveStatic: bool
  resetPoint: th.Vf2
  toPortal: str

  saves: []ChumState
  showDebug: bool = false
  lastUpdateTime: int = 0
  startTimer: real
  touchTimer: real
  ended: bool
  frameSkips*: int
)

fn getCameraRect(): rect.Rect {
  return rect.mk(chara.rect.x+chara.rect.w/2-d_global.screen.w/2, chara.rect.y+chara.rect.h/2-d_global.screen.h/2, d_global.screen.w, d_global.screen.h)
}

fn findGuideInPoint(p: th.Vf2): rect.Rect {
  for i, guide in guides {
    if d_util.rectVsPoint(guide, p) {
      return guide
    }
  }

  return getCameraRect()
}

fn getCameraTargetRect(): rect.Rect {
  guide := findGuideInPoint(chara.rect.getPos())
  cameraRect := getCameraRect()
  scale := d_util.min(guide.w/cameraRect.w, guide.h/cameraRect.h)
  maxScale := 1.0/(d_global.screen.w/640)
  if scale > maxScale {
    scale = maxScale
  }

  // put at center
  cameraRect.w *= scale
  cameraRect.h *= scale
  cameraRect.x = chara.rect.x+chara.rect.w/2-cameraRect.w/2
  cameraRect.y = chara.rect.y+chara.rect.h/2-cameraRect.h/2

  return d_util.rectKeepInBounds(cameraRect, guide)
}

type CameraProperties = struct {
  pos: th.Vf2
  scale: real
}

fn getCameraProperties*(): CameraProperties {
  targetRect := getCameraTargetRect()
  return CameraProperties{targetRect.getPos(), d_util.min(d_global.screen.w/targetRect.w, d_global.screen.h/targetRect.h)}
}

fn cameraControl() {
  props := getCameraProperties()

  camera = d_rerp.iVf2(camera, props.pos, updateStep*10.0)
  cameraScale = d_rerp.iReal(cameraScale, props.scale, updateStep*10.0)
}

fn cameraOffsetPoint(p: th.Vf2): th.Vf2 {
  p.x -= camera.x
  p.y -= camera.y
  p.x *= cameraScale
  p.y *= cameraScale
  return p
}

fn cameraOffsetRect(r: rect.Rect): rect.Rect {
  r.x -= camera.x
  r.y -= camera.y
  r.x *= cameraScale
  r.y *= cameraScale
  r.w *= cameraScale
  r.h *= cameraScale
  return r
}

fn playerControl() {
  if input.isPressed(input.key_up) && chara.onGround {
    chara.vel.y = -3.65
  }
  if input.isPressed(input.key_left) {
    chumDir = 0
    chumWalkTime += updateStep
    chara.vel.x = -2.5
  }
  if input.isPressed(input.key_right) {
    chumDir = 1
    chumWalkTime += updateStep
    chara.vel.x = 2.5
  } 
  if !input.isPressed(input.key_left) && !input.isPressed(input.key_right) {
    chumWalkTime = 0
  }

  if chara.rect.y > 2e3 {
    // NOTE: Respawn
    chara.vel.x = 0
    chara.vel.y = 0
    chara.rect.x = world.playerSpawnPos.x * world.tileset.tileSizeX
    chara.rect.y = world.playerSpawnPos.y * world.tileset.tileSizeY
  }
}

fn entPhysics(ent: ^Character) {
  gravity := updateStep*60.0*0.1

  ent.vel.y += gravity

  if ent.vel.y > 10 {
    ent.vel.y = 10
  }
  

  ent.rect.x += ent.vel.x*updateStep*60.0
  ent.rect.y += ent.vel.y*updateStep*60.0

  ent.onGround = false
  ent.onSlope = false
  ent.vel.x = 0
}

fn collCalcRectSnap(r, along: rect.Rect): th.Vf2 {
  x_left := r.x + r.w - along.x
  y_up := r.y + r.h - along.y

  x_right := along.x + along.w - r.x
  y_down := along.y + along.h - r.y

  var (min_x: real; min_y: real)

  if fabs(x_left) < fabs(x_right) {
    min_x = -x_left
  } else {
    min_x = x_right
  }

  if fabs(y_up) < fabs(y_down) {
    min_y = -y_up
  } else {
    min_y = y_down
  }

  if fabs(min_x) < fabs(min_y) {
    return th.Vf2{min_x, 0}
  } 
  return th.Vf2{0, min_y}
}

fn collSortShapesByProximity(shapes: ^[]Shape, along: th.Vf2) {
  for i := 0; i < len(shapes^); i++ {
    closest := &shapes[i]
    mindist := real(99999999)
    for j := i; j < len(shapes^); j++ {
      current := shapes[j].rect
      point := th.Vf2{current.x + current.w / 2, current.y + current.h / 2}
      dist := point.distanceTo(along)
      if dist < mindist {
        mindist = dist
        closest = &shapes[j]
      }
    }
    tmp := closest^
    closest^ = shapes[i]
    shapes[i] = tmp
  }
}

fn entVsShape(ent: ^Character, s: Shape) {
  r := s.rect
  snap := collCalcRectSnap(ent.rect, r)

  // Not a valid collision (probably pushed back by other blocks?)
  if d_util.rectVsRect(ent.rect, r) == false {
    return
  }
  
  switch s.shape {
  case shapeLeftUpSlope:
    cornerX := ent.rect.x + ent.rect.w - r.x;
    if cornerX > r.w {
      cornerX = r.w
    }
    cornerY := ent.rect.y + ent.rect.h - r.y;
    hitY := r.h - ((r.h/r.w) * (cornerX))
    if hitY > r.h {
      hitY = r.h
    }

    
    if cornerY > hitY {
      ent.vel.y = 0;
      ent.onGround = true;
      ent.onSlope = true;
      ent.rect.y += hitY-cornerY;
    }
  case shapeRightUpSlope:
    cornerX := ent.rect.x - r.x;
    if cornerX < 0 {
      cornerX = 0;
    }
    cornerY := ent.rect.y + ent.rect.h - r.y;
    hitY := (r.h/r.w) * (cornerX)

    if cornerY > hitY {
      ent.vel.y = 0;
      ent.onGround = true;
      ent.onSlope = true;
      ent.rect.y += hitY-cornerY;
    }
  case shapeLeftDownSlope:
    cornerX := ent.rect.x + ent.rect.w - r.x;
    if cornerX > r.w {
      cornerX = r.w
    }
    cornerY := ent.rect.y - r.y;
    hitY := (r.h/r.w) * (cornerX)

    if cornerY < hitY {
      if ent.vel.y < 0 {
        ent.vel.y = 0;
      }
      ent.rect.y += hitY-cornerY;
    }
  case shapeRightDownSlope:
    cornerX := ent.rect.x - r.x;
    if cornerX < 0 {
      cornerX = 0;
    }
    cornerY := ent.rect.y - r.y;
    hitY := r.h - ((r.h/r.w) * (cornerX))

    if cornerY < hitY {
      if ent.vel.y < 0 {
        ent.vel.y = 0;
      }
      ent.rect.y += hitY-cornerY;
    }
  case shapeBlock:
    if snap.x != 0 {
      ent.vel.x = 0
    } else if (snap.y > 0 && ent.vel.y < 0) || (snap.y < 0 && ent.vel.y > 0) {
      ent.vel.y = 0
    }

    if snap.y < 0 {
      ent.onGround = true
    }
    ent.rect.x += snap.x
    ent.rect.y += snap.y
  }
}

fn entConstructCollidingSolids(ent: ^Character): []Shape {
  tangents := world.getTangentTiles(rect.Rect{ent.rect.x-1, ent.rect.y-1, ent.rect.w+1, ent.rect.h+1})
  solids := []Shape{}
  nonBlockSolids := []Shape{}

  for i, tangent in tangents {
    shapeRect := rect.Rect{tangent.x*world.tileset.tileSizeX, tangent.y*world.tileset.tileSizeY, world.tileset.tileSizeX, world.tileset.tileSizeY} 
    shapeType := -1

    switch tangent.id {
      // slab
      case 25: shapeType = shapeBlock
        shapeRect.y += world.tileset.tileSizeY/2
        shapeRect.h /= 2
      case 30: if toggleActive { shapeType = shapeBlock; }
      case 31: if !toggleActive { shapeType = shapeBlock; }
      case 1, 5, 7, 29:   shapeType = shapeBlock
      case 9:   shapeType = shapeLeftUpSlope
      case 10:  shapeType = shapeRightUpSlope
      case 13:  shapeType = shapeLeftDownSlope
      case 14:  shapeType = shapeRightDownSlope
      case 15:  shapeType = shapeLeftUpSlope
          shapeRect.h /= 2;
      case 16:  shapeType = shapeRightUpSlope
          shapeRect.h /= 2;
      case 4:   shapeType = shapeLeftUpSlope
          shapeRect.y += world.tileset.tileSizeY/2;
          shapeRect.h /= 2;
      case 8:   shapeType = shapeRightUpSlope
          shapeRect.y += world.tileset.tileSizeY/2;
          shapeRect.h /= 2;
      case 11:  shapeType = shapeRightDownSlope
          shapeRect.h /= 2;
      case 12:  shapeType = shapeLeftDownSlope
          shapeRect.h /= 2;
    }
    
    if shapeType == shapeBlock {
      solids = append(solids, Shape{shapeRect, shapeType})
    } else {
      nonBlockSolids = append(nonBlockSolids, Shape{shapeRect, shapeType})
    }
  }


  solids = append(solids, nonBlockSolids)
  collSortShapesByProximity(&solids, d_util.rectCenter(ent.rect))
  return solids
}

fn entCheckCollision(ent: ^Character): bool {
  r := ent.rect
  r.w -= 1
  solids := entConstructCollidingSolids(ent)
  for i, shape in solids {
    if shape.shape == shapeBlock {
      if d_util.rectVsRect(r, shape.rect) {
        return true
      }
    }
    // TODO: Other shapes
  }

  return false
}

fn entSolveCollision(ent: ^Character) {
  solids := entConstructCollidingSolids(ent)

  for i, solid in solids {
    entVsShape(ent, solid)
  }
}

fn _drawRectTangents(r: rect.Rect) {
  solids := entConstructCollidingSolids(&chara)

  for i, solid in solids {
    rec := cameraOffsetRect(solid.rect)
    switch solid.shape {
    case shapeBlock:
      canvas.drawRect(0x00FF0055, rec)
    case shapeLeftUpSlope, shapeRightDownSlope:
      canvas.drawLine(0x00FF0055, th.Vf2{rec.x, rec.y+rec.h}, th.Vf2{rec.x+rec.w, rec.y}, 2)
    case shapeRightUpSlope, shapeLeftDownSlope:
      canvas.drawLine(0x00FF0055, th.Vf2{rec.x, rec.y}, th.Vf2{rec.x+rec.w, rec.y+rec.h}, 2)
    }
    canvas.drawText(sprintf("%v", i), th.Vf2{rec.x, rec.y}, th.white, 1)
  }
}

fn convertEntityDescToCharacter(ent: d_worlddata.EntityDesc): Character {
  return Character{id: ent.id, tags: initTagSetFromString(ent.tags), rect: ent.rect}
}

fn findEntity*(id: str): ^Character {
  for i in entities {
    if entities[i].id == id {
      return &entities[i]
    }
  }

  return null
}

fn start*(worldData: d_worlddata.WorldData) {
  toPortal = ""
  startTimer = 0.5
  touchTimer = 5
  lastUpdateTime = th.time
  toggleActive = false
  toggleActiveStatic = false
  lockInput = false

  world = worldData.world

  chara = Character{id: ">>player", tags: initTagSetFromString("Player"), rect: rect.Rect{world.playerSpawnPos.x*world.tileset.tileSizeX, world.playerSpawnPos.y*world.tileset.tileSizeY, world.tileset.tileSizeX/1.2, world.tileset.tileSizeY}}

  resetPoint.x = 19*32
  resetPoint.y = -41*32
  chumRotTime = 0
  chumWalkTime = 0
  chumDir = 0

  entities = make([]Character, 0)
  guides = make([]rect.Rect, 0)

  for i, ent in worldData.entities {
    chr := convertEntityDescToCharacter(ent)
    chr.originalRect = chr.rect
    if chr.tags.isSet("Comment") {
      continue;
    }
    if chr.tags.isSet("Guide") {
      guides = append(guides, chr.rect)
    } else {
      entities = append(entities, chr)
    }
  }

  saves = []ChumState{}
  camera = getCameraProperties().pos
  cameraScale = getCameraProperties().scale
}

fn init*(rl: ^d_resources.ResourceLoader) {
  imgObj = d_resources.loadImage(rl, "data/img/obj_interactive.png")
  imgObj.setfilter(1)
  imgChum = d_resources.loadImage(rl, "data/img/chr_chum.png")
  imgChum.setfilter(1)
}

fn getCollidingEntities(against: ^Character): []^Character {
  collided := []^Character{}
  if d_util.rectVsRect(chara.rect, against.rect) {
    collided = append(collided, &chara)
  }

  for i in entities {
    // Prevent self collision
    ent := &entities[i]
    if ent == against {
      continue
    }
    if d_util.rectVsRect(ent.rect, against.rect) {
      collided = append(collided, ent)
    }
  }
  return collided
}

fn updateEntities() {
  entPhysics(&chara)
  entSolveCollision(&chara)
 
  if chara.vel.y > 3 {
    chumRotTime += updateStep*100.0
  }
  if chara.vel.y <= 0 {
    chumRotTime = 0
  }
}

fn drawEntityCustomRect(ent: ^Character, r: rect.Rect) {
  destRect := d_util.rectShrink(cameraOffsetRect(r), -2*cameraScale)

  color := th.blue
  if ent.onGround {
    color = th.red
  }

  canvas.drawRect(color, cameraOffsetRect(ent.rect))
}

const (
  cropBoulder = rect.Rect{0, 0, 64, 128}
  cropLeverOn  = rect.Rect{64, 64, 64, 64}
  cropLeverOff = rect.Rect{64, 0, 64, 64}
  cropLeverStaticOn  = rect.Rect{256, 64, 64, 64}
  cropLeverStaticOff = rect.Rect{256, 0, 64, 64}
  cropTele     = rect.Rect{128+64, 0, 64, 64}
  cropTeleOff  = rect.Rect{128+64, 64, 64, 64}
  cropResetV   = rect.Rect{256+64, 0, 64, 64}
  cropResetH   = rect.Rect{256+64, 64, 64, 64}
  cropControls = rect.Rect{0, 128, 256, 256}
)

fn drawEntity(ent: ^Character) {
  destRect := d_util.rectShrink(cameraOffsetRect(ent.rect), -2*cameraScale)
  if toPortal == ent.id {
    destRect.x += cos(th.time/1000.0*6)*5*cameraScale
    destRect.y += sin(th.time/1000.0*6)*5*cameraScale
  }

  if ent.tags.isSet("lever") {
    if toggleActive {
      d_util.drawImageRect(imgObj, cropLeverOn, destRect)
    } else {
      d_util.drawImageRect(imgObj, cropLeverOff, destRect)
    }
  } else if ent.tags.isSet("lever_s") {
    if toggleActiveStatic {
      d_util.drawImageRect(imgObj, cropLeverStaticOn, destRect)
    } else {
      d_util.drawImageRect(imgObj, cropLeverStaticOff, destRect)
    }
  } else if ent.tags.isSet("boulder") {
    d_util.drawImageRect(imgObj, cropBoulder, destRect)
  } else if ent.tags.isSet("tele") {
    if !entCheckCollision(ent) {
      d_util.drawImageRect(imgObj, cropTele, destRect)
    } else {
      d_util.drawImageRect(imgObj, cropTeleOff, destRect, 0, 0xFF0000FF)
    }
  } else if ent.tags.isSet("tele_off") {
    d_util.drawImageRect(imgObj, cropTeleOff, destRect)
  } else if ent.tags.isSet("reset") {
    if (destRect.w < destRect.h) {
      d_util.drawImageRect(imgObj, cropResetV, destRect)
    } else {
      d_util.drawImageRect(imgObj, cropResetH, destRect)
    }
  } else if ent.tags.isSet("portal") {
    canvas.drawRect(0xc2FFF633, d_util.rectShrink(destRect, -10*cameraScale))
    d_background.drawBackgroundSquare(destRect, th.time/1000.0, 0xC2FFF655, 0x526c7aFF, 1)

  } else if ent.tags.isSet("controls") {
    d_util.drawImageRect(imgObj, cropControls, destRect)
  }  else {
    drawEntityCustomRect(ent, ent.rect)
  }
}

const (
  chumStateStand = 0
  chumStateJump = 1
  chumStateFall = 2
  chumStateWalk = 3
  chumStateWalk2 = 4
  chumFaceRect = rect.Rect{0, 0, 94, 107}
)

fn drawChumState(state: int, chum: ChumState, tint: uint32) {
  at := chum.ent.rect.getPos()
  bodyRect := rect.mk(chumFaceRect.x+chumFaceRect.w*(state+1), chumFaceRect.y, chumFaceRect.w, chumFaceRect.h)
  chumDestRect := cameraOffsetRect(rect.mk(at.x-10, at.y-20, chumFaceRect.w/2, chumFaceRect.h/2))
  if chum.dir == 0 {
    // Mirror.
    chumDestRect.x += chumDestRect.w;
    chumDestRect.w *= -1;
  }
  d_util.drawImageRect(imgChum, bodyRect, chumDestRect, chum.rotTime, tint)
  d_util.drawImageRect(imgChum, chumFaceRect, chumDestRect, chum.rotTime, tint)
  if showDebug {
    canvas.drawText(sprintf("%d %d", trunc(at.x/32), trunc(at.y/32)), chumDestRect.getPos(), th.red, 1)
  }
}

fn loadStateToChar(state: ChumState) {
  chara = state.ent
  chumDir = state.dir
  chumWalkTime = state.walkTime
  chumRotTime = state.rotTime
  toggleActiveStatic = state.toggleActiveStatic
}

fn getCharState(): ChumState {
  return ChumState{
    dir: chumDir,
    walkTime: chumWalkTime,
    rotTime: chumRotTime,
    ent: chara,
    toggleActiveStatic: toggleActiveStatic}
}

fn drawChum(state: int) {
  drawChumState(state, getCharState(), 0xFFFFFFFF)
}

fn getChumSpriteState(chum: ChumState): int {
  chara := &chum.ent

  if chara.vel.y > 0 {
    return chumStateFall
  } else if !chara.onGround {
    return chumStateJump
  } else if chum.walkTime == 0.0 {
    return chumStateStand
  } else {
    switch trunc(chum.walkTime*8.0) % 3 {
      case 0: return chumStateJump
      case 1: return chumStateStand
      case 2: return chumStateJump
    }
  }
  return -1
}

fn drawEntities() {
  for i, entity in entities {
    drawEntity(&entities[i])
  }
  
  drawChum(getChumSpriteState(getCharState()))
}

fn drawEntityDebugRects() {
  for i, entity in entities {
    canvas.drawRect(th_color.alpha(th.blue, 0.4), cameraOffsetRect(entity.rect)) 
  }
  canvas.drawRect(th_color.alpha(th.blue, 0.4), cameraOffsetRect(chara.rect)) 
}

fn canActivateLever(): bool {
  can := true
  toggleActive = !toggleActive

  if (entCheckCollision(&chara)) {
    can = false
  }

  toggleActive = !toggleActive
  return can
}

fn canActivateStaticLever(): bool {
  can := true
  toggleActiveStatic = !toggleActiveStatic

  if (entCheckCollision(&chara)) {
    can = false
  }

  toggleActiveStatic = !toggleActiveStatic
  return can
}

fn update*() {
  toPortal = ""
  touchTimer -= th.delta/1000.0
  if ended {
    startTimer += th.delta/1000.0
  } else {
    startTimer -= th.delta/1000.0
  }
  if startTimer < 0 {
    startTimer = 0
  }
  if startTimer > 1 && ended {
    d_global.end = true
  }

  if startTimer > 0 && !ended {
    lastUpdateTime = th.time
    return
  }
  if startTimer > 0.4 && ended {
    lastUpdateTime = th.time
    return
  }

  toggleActiveVal = d_rerp.iReal(toggleActiveVal, real(int(toggleActive)), th.delta/100.0)

  iterations := (th.time - lastUpdateTime) / updateStepMs
  lastUpdateTime += iterations * updateStepMs

  if iterations > 120 {
    frameSkips += iterations-120
    iterations = 120
  } 

  for d := 0; d < iterations; d++ {
    playerControl()

    // UPDATE PLAYER & CAMERA //
    updateEntities()
    cameraControl()

    if input.isPressed(input.key_alt) && input.isJustPressedc('d') {
      showDebug = !showDebug
    }
  }
 
  if !lockInput {
    if input.isJustPressedc('z') {
      if len(saves) > 0 {
        if !entCheckCollision(&saves[len(saves)-1].ent) {
          loadStateToChar(saves[len(saves)-1])
          saves = delete(saves, len(saves)-1)
        }
      }
    }

    if input.isJustPressedc('x') {
      if len(saves) < 1 {
        saves = append(saves, getCharState())
      }
    }
 
    if input.isJustPressedc('r') {
      chara.rect.x = resetPoint.x
      chara.rect.y = resetPoint.y
      toggleActive = false
      toggleActiveStatic = false
      saves = {}
    }


    for i := 0; i < len(entities); i++ {
      ent := &entities[i]

      if ent.tags.isSet("reset") {
        /*
        if d_util.rectVsRect(ent.rect, chara.rect) {
          saves = []ChumState{}
          toggleActive = false
          toggleActiveStatic = false
        }
        */
        if d_util.rectVsRect(ent.rect, chara.rect) {
          entShape := Shape{rect: ent.rect}
          if (ent.rect.w > ent.rect.h) {
            if ((chara.rect.y + chara.rect.h) < (ent.rect.y+ent.rect.h))  {
              chara.rect.y = ent.rect.y - chara.rect.h+1
              entVsShape(&chara, entShape)
              chumRotTime = 0
            } else {
              ent.vel.y += 4
              resetPoint.x = ent.rect.x+ent.rect.w/2
              resetPoint.y = ent.rect.y-ent.rect.h/2

              toggleActive = false
              toggleActiveStatic = false
              saves = {}
            }
          } else {
            if (chara.rect.x > ent.rect.x)  {
              chara.rect.x = ent.rect.x + ent.rect.w-1
              entVsShape(&chara, entShape)
              chumRotTime = 0
            } else {
              ent.vel.x += 4
              resetPoint.x = ent.rect.x+ent.rect.w/2
              resetPoint.y = ent.rect.y+ent.rect.h/2

              toggleActive = false
              toggleActiveStatic = false
              saves = {}
            }
          }
        }
      }
      if ent.tags.isSet("boulder") {
        power := real(int(toggleActiveStatic))
        boulderTargetRect := d_util.rectMove(ent.originalRect, th.Vf2{0, -ent.originalRect.h*power})
        ent.rect = d_rerp.iRect(ent.rect, boulderTargetRect, th.delta/100.0)

        // Resolve player collision
        entShape := Shape{rect: ent.rect}
        entVsShape(&chara, entShape)
      }
      if ent.tags.isSet("lever") {
        if d_util.rectVsRect(ent.rect, chara.rect) {
          if input.isJustPressedc(' ') {
            if canActivateLever() {
              toggleActive = !toggleActive
            }
          } 
        }
      }
      if ent.tags.isSet("lever_s") {
        if d_util.rectVsRect(ent.rect, chara.rect) {
          if input.isJustPressedc(' ') {
            if canActivateStaticLever() {
              toggleActiveStatic = !toggleActiveStatic
            }
          } 
        }
      }
      if ent.tags.isSet("tele") {
        if d_util.rectVsRect(ent.rect, chara.rect) {
          dest := ent.tags.param("tele")
          ent := findEntity(dest)
          toPortal = dest
          if ent == null {
            mdbg.logt("Error: Destination entity doesn't exist")
          } else if input.isJustPressedc(' ') {
            if !entCheckCollision(ent) {
              chara.rect.x = ent.rect.x
              chara.rect.y = ent.rect.y
              input.clearc(' ')
            } 
          }
        }
      }
        
      if ent.tags.isSet("portal") && touchTimer < 0 {
        if d_util.rectVsRect(ent.rect, chara.rect) {
          ended = true
        }
      }
    }
  }


}

fn drawGuideRects() {
  for i, guide in guides {
    d_util.drawRectLines(th.yellow, cameraOffsetRect(guide))
  }
}

fn drawOutlines() {
  radiusPulsing := ((sin(th.time/1000.0*2.0)+2)/2.0)*3+1
  radiusNormal := 3

  renderTiles := world.collectLayerRenderTiles(cameraOffsetPoint({0, 0}), 0, cameraScale)

  for i, renderTile in renderTiles {
    if renderTile.id == 29 || (toggleActive && renderTile.id == 30) || (renderTile.id == 31 && !toggleActive) {
      radius := radiusPulsing
      if renderTile.id == 29 {
        radius = radiusNormal
      }
      canvas.drawRect(0xAAAAFF99, d_util.rectShrink(renderTile.rect, -radius*cameraScale))
      canvas.drawRect(0xAAAAFF22, d_util.rectShrink(renderTile.rect, 2*-radius*cameraScale))
    }
  }
}

fn drawOverlays() {
  renderTiles := world.collectLayerRenderTiles(cameraOffsetPoint({0, 0}), 0, cameraScale)
  colorA := th_color.alpha(0xFFFFFFFF, d_util.clamp(toggleActiveVal+0.1, 0, 1))
  colorB := th_color.alpha(0xFFFFFFFF, d_util.clamp(1-toggleActiveVal+0.1, 0, 1))

  for i, renderTile in renderTiles {
    switch renderTile.id {
      case 30: canvas.drawRect(colorA, renderTile.rect)
      case 31: canvas.drawRect(colorB, renderTile.rect)
      default:
    }
  }
}

fn draw*() {
  // RENDER BG //
  bgCam := rect.mk(camera.x, camera.y, d_global.screen.w/cameraScale, d_global.screen.h/cameraScale)
  bgCam.x /= 2
  bgCam.y /= 2
  oldScale := cameraScale 
  oldPos := camera
  d_background.draw(bgCam)

  if startTimer > 0 {
    cameraScale *= ((startTimer*2)+1)
    camera.x = d_rerp.iReal(camera.x, chara.rect.x-d_global.screen.w/2/cameraScale, startTimer)
    camera.y = d_rerp.iReal(camera.y, chara.rect.y-d_global.screen.h/2/cameraScale, startTimer)
  }


  // RENDER WORLD //
  world.drawLayer(cameraOffsetPoint({0, 0}), -1, cameraScale)
  drawOutlines()
  world.drawLayer(cameraOffsetPoint({0, 0}), 0, cameraScale)
  drawOverlays()
  drawEntities()
    
  for i, state in saves {
    if !entCheckCollision(&state.ent) {
      drawChumState(getChumSpriteState(state), state, 0xFFFFFF44)
    } else {
      drawChumState(getChumSpriteState(state), state, 0xFF000044)
    }
  }

  world.drawLayer(camera.mulf(-cameraScale), 1, cameraScale)


  // DEBUG OVERYALS //
  if showDebug {
    _drawRectTangents(chara.rect)
    drawGuideRects()
    d_util.drawRectLines(th.green, cameraOffsetRect(getCameraTargetRect()))
    drawEntityDebugRects()
  }

  cameraScale = oldScale
  camera = oldPos
}
